@*
 * Copyright Â© 2015-2019 the contributors (see Contributors.md).
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Gets all the properties of a resource, along with the properties of its file values, but not including subproperties
 * of knora-base:hasValue, using standard SPARQL, without inference.
 *
 * This template is used only by getResourceInfo.scala.txt.
 *
 * @param triplestore the name of the triplestore being used.
 * @param resourceIri the IRI of the resource to be queried.
 *@
@(triplestore: String,
  resourceIri: IRI)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?prop ?obj ?isFileValue ?objPred ?objObj
WHERE {
    BIND(IRI("@resourceIri") AS ?resource)

    ?resource rdf:type ?resourceClass ;
        knora-base:isDeleted false .

    ?resourceClass rdfs:subClassOf* knora-base:Resource .

    {
        @* Query statements about the resource's file values. *@

        ?prop rdfs:subPropertyOf* knora-base:hasFileValue .
        BIND(true AS ?isFileValue)

        ?resource ?prop ?obj .
        ?obj knora-base:isDeleted false .
        ?obj ?objPred ?objObj .
    }
    UNION
    {
        @* Query other statements about the resource. *@

        ?resource ?prop ?obj .

        FILTER NOT EXISTS {
            ?prop rdfs:subPropertyOf* knora-base:resourceProperty .
        }
    }
}
