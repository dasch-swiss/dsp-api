@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and André Fatton.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Gets information about the incoming references to a resource, as well as about the referring resources.
 *
 * @param resourceIri the IRI of the resource.
 *@
@(resourceIri: IRI)

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?referringResource ?linkingProp ?p ?o ?objPred ?objObj
WHERE {
    BIND(IRI("@resourceIri") as ?resource)

    MINUS {
        ?resource knora-base:isDeleted true .
    }

    ?linkingProp rdfs:subPropertyOf* knora-base:hasLinkTo .

    MINUS {
        ?linkingProp rdfs:subPropertyOf+ knora-base:isPartOf .
    }

    ?referringResource ?linkingProp ?resource .

    MINUS {
        ?referringResource knora-base:isDeleted true .
    }

    {
        ?referringResource ?p ?o .

        MINUS {
            ?p rdfs:subPropertyOf+ knora-base:hasValue .
        }
    }
    UNION
    {
        ?p rdfs:subPropertyOf* knora-base:hasFileValue .
        ?referringResource ?p ?o .

        ?o ?objPred ?objObj .
    }

    MINUS {
        ?o knora-base:isDeleted true .
    }
}
