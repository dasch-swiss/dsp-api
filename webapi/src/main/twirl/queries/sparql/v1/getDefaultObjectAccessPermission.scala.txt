@*
 * Copyright Â© 2015-2019 the contributors (see Contributors.md).
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Gets the administrative permission object identified by project and either group / resource class / property.
 *
 * @param triplestore the name of the triplestore being used.
 * @param projectIri the project's IRI.
 * @param groupIri the group's IRI.
 * @param resourceClassIri the resource's class IRI.
 * @param propertyIri the property's IRI.
 *@
@(triplestore: String,
  projectIri: IRI,
  maybeGroupIri: Option[IRI],
  maybeResourceClassIri: Option[IRI],
  maybePropertyIri: Option[IRI])

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?s ?p ?o

@* Ensure that inference is not used in this query. *@
@if(triplestore.startsWith("graphdb")) {
    FROM <http://www.ontotext.com/explicit>
}

WHERE {
    ?s rdf:type knora-base:DefaultObjectAccessPermission .
    ?s knora-base:forProject <@projectIri> .

    @if(maybeGroupIri.nonEmpty) {
        ?s knora-base:forGroup <@maybeGroupIri.get> .
    }

    @if(maybeResourceClassIri.nonEmpty) {
        ?s knora-base:forResourceClass <@maybeResourceClassIri.get> .
    } else {
        OPTIONAL { ?s knora-base:forResourceClass ?class } .
        FILTER ( !bound(?class) )
    }

    @if(maybePropertyIri.nonEmpty) {
        ?s knora-base:forProperty <@maybePropertyIri.get> .
    } else {
        OPTIONAL { ?s knora-base:forProperty ?prop } .
        FILTER ( !bound(?prop) )
    }

    ?s ?p ?o .
}