@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and André Fatton.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Given the IRI of a resource, gets information about the resources that are part of that resource.
 *
 * This template is used only by getContext.scala.txt.
 *
 * Since the triplestore type is GraphDB, we assume that inference is enabled, and we use it to optimise the generated
 * SPARQL. Specifically, we use inference to return search results matching subproperties of Knora base properties
 * such as knora-base:isPartOf. Without inference, we have to do this using SPARQL property path syntax
 * (rdfs:subPropertyOf*). Using inference requires us to use GraphDB's GRAPH <http://www.ontotext.com/explicit>
 * whenever we need to get explicit (non-inferred) statements.
 *
 * @param triplestore the name of the triplestore being used.
 * @param resourceIri the IRI of the resource.
 *@
@(resourceIri: IRI)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?sourceObject ?sourceObjectAttachedToUser ?sourceObjectAttachedToProject ?sourceObjectPermissions ?seqnum ?linkValue ?linkValueOwner ?linkValueProject ?linkValuePermissions ?firstprop ?fileValue ?isPreview ?internalFilename ?internalMimeType ?originalFilename ?dimX ?dimY ?qualityLevel ?fileValuePermissions ?fileValueAttachedToUser ?fileValueAttachedToProject
WHERE {
    BIND(IRI("@resourceIri") as ?resource)

    ?resource rdf:type knora-base:Resource ;
         knora-base:isDeleted false .

    # Find something that relates to this resource via knora-base:isPartOf.

    ?sourceObject knora-base:isPartOf ?resource ;
        knora-base:isDeleted false .

    GRAPH <http://www.ontotext.com/explicit> {
        ?sourceObject ?linkingProp ?resource .
    }

    OPTIONAL {
        ?sourceObject knora-base:seqnum ?seqnumVal .
        ?seqnumVal knora-base:valueHasInteger ?seqnum .
    }

    ?sourceObject rdfs:label ?firstprop ;
        knora-base:attachedToUser ?sourceObjectAttachedToUser ;
        knora-base:attachedToProject ?sourceObjectAttachedToProject .

    OPTIONAL {
        ?sourceObject knora-base:hasPermissions ?sourceObjectPermissions .
    }

    ?sourceObject knora-base:hasLinkToValue ?linkValue .

    ?linkValue rdf:type knora-base:LinkValue ;
        rdf:subject ?sourceObject ;
        rdf:predicate ?linkingProp ;
        rdf:object ?resource ;
        knora-base:isDeleted false ;
        knora-base:attachedToUser ?linkValueOwner .

    OPTIONAL {
        ?linkValue knora-base:attachedToProject ?linkValueProject .
    }

    OPTIONAL {
        ?linkValue knora-base:hasPermissions ?linkValuePermissions .
    }

    OPTIONAL {
        ?sourceObject knora-base:hasFileValue ?fileValue .

        ?fileValue a knora-base:StillImageFileValue ;
             knora-base:isDeleted false .

        OPTIONAL {
            ?fileValue knora-base:isPreview ?isPreview .
        }

        ?fileValue knora-base:internalMimeType ?internalMimeType ;
                 knora-base:originalFilename ?originalFilename ;
                 knora-base:internalFilename ?internalFilename ;
                 knora-base:dimX ?dimX ;
                 knora-base:dimY ?dimY ;
                 knora-base:qualityLevel ?qualityLevel .

        ?fileValue knora-base:attachedToUser ?fileValueAttachedToUser .

        OPTIONAL {
            ?fileValue knora-base:attachedToProject ?fileValueAttachedToProject .
        }

        OPTIONAL {
            ?fileValue knora-base:hasPermissions ?fileValuePermissions .
        }
    }
}
ORDER BY ?seqnum ?sourceObject ?isPreview
