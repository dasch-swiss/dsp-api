@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and André Fatton.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Given the IRI of a resource, gets information about the resources that are part of that resource.
 *
 * @param resourceIri the IRI of the resource.
 *@
@(resourceIri: IRI)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?seqnum ?sourceObject ?sourceObjectAttachedToUser ?sourceObjectAttachedToProject (GROUP_CONCAT(DISTINCT(?sourceObjectPermissionAssertion); separator=";") AS ?sourceObjectPermissionAssertions) ?firstprop ?preview ?internalFilename ?internalMimeType ?originalFilename ?dimX ?dimY ?qualityLevel (GROUP_CONCAT(DISTINCT(?previewPermissionAssertion); separator=";") AS ?previewPermissionAssertions) ?previewAttachedToUser ?previewAttachedToProject
WHERE {
    BIND(IRI("@resourceIri") as ?resource)

    ?resource rdf:type ?resourceClass .

    MINUS {
        ?resource knora-base:isDeleted true .
    }

    ?resourceClass rdfs:subClassOf+ knora-base:Resource .

    # Find something that relates to this resource via partOf.

    ?linkingProp rdfs:subPropertyOf+ knora-base:isPartOf .
    ?seqProp rdfs:subPropertyOf+ knora-base:seqnum .

    ?sourceObject ?linkingProp ?resource .

    ?sourceObject ?seqProp ?seqnumVal .
    ?seqnumVal knora-base:valueHasInteger ?seqnum .
    ?sourceObject rdfs:label ?firstprop .

    ?permission rdfs:subPropertyOf+ knora-base:hasPermission .
    ?sourceObject ?permission ?sourceObjectPermissionGroup .
    BIND(CONCAT(STR(?permission), ",", STR(?sourceObjectPermissionGroup)) as ?sourceObjectPermissionAssertion)

    ?sourceObject knora-base:attachedToUser ?sourceObjectAttachedToUser .
    ?sourceObject knora-base:attachedToProject ?sourceObjectAttachedToProject .

    OPTIONAL {
        ?fileValueProp rdfs:subPropertyOf* knora-base:hasFileValue .
        ?sourceObject ?fileValueProp ?preview .

        ?preview a knora-base:StillImageFileValue .
        ?preview knora-base:isPreview true .

        MINUS {
            ?preview knora-base:isDeleted true .
        }

        ?preview knora-base:internalMimeType ?internalMimeType ;
                 knora-base:originalFilename ?originalFilename ;
                 knora-base:internalFilename ?internalFilename ;
                 knora-base:dimX ?dimX ;
                 knora-base:dimY ?dimY ;
                 knora-base:qualityLevel ?qualityLevel .

        ?preview ?permission ?previewPermissionGroup .
        BIND(CONCAT(STR(?permission), ",", STR(?previewPermissionGroup)) as ?previewPermissionAssertion)

        ?preview knora-base:attachedToUser ?previewAttachedToUser .

        OPTIONAL {
            ?preview knora-base:attachedToProject ?previewAttachedToProject .
        }

    }

    MINUS {
        ?sourceObject knora-base:isDeleted true .
    }
}

GROUP BY
    ?seqnum
    ?sourceObject
    ?sourceObjectAttachedToUser
    ?sourceObjectAttachedToProject
    ?firstprop
    ?preview
    ?internalFilename
    ?internalMimeType
    ?originalFilename
    ?dimX
    ?dimY
    ?qualityLevel
    ?previewAttachedToUser
    ?previewAttachedToProject


ORDER BY ?seqnum ?sourceObject