@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and André Fatton.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi._

@**
 * Gets the outbound or inbound links from/to a resource, using standard SPARQL, without inference. This query is used
 * recursively to get a graph of resources reachable from a given resource.
 *
 * This template is used only by getGraphData.scala.txt.
 *
 * @param triplestore the name of the triplestore being used.
 * @param startNodeIri the IRI of the resource to use as the starting point of the query.
 * @param startNodeOnly if true, returns information only about the start node.
 * @param outbound true to get outbound links, false to get inbound links.
 *@
@(triplestore: String,
  startNodeIri: IRI,
  startNodeOnly: Boolean,
  outbound: Boolean = true)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?node ?nodeClass ?nodeLabel ?nodeCreator ?nodeProject ?nodePermissions
       ?linkValue ?linkProp ?linkValueCreator ?linkValuePermissions
WHERE {
    @if(startNodeOnly) {
        BIND(IRI("@startNodeIri") AS ?node) .

        ?node a ?nodeClass ;
            rdfs:label ?nodeLabel ;
            knora-base:attachedToUser ?nodeCreator ;
            knora-base:attachedToProject ?nodeProject ;
            knora-base:isDeleted false .

        OPTIONAL {
            ?node knora-base:hasPermissions ?nodePermissions .
        }
    } else {
        BIND(IRI("@startNodeIri") AS ?startNode) .

        ?linkProp rdfs:subPropertyOf* knora-base:hasLinkTo .

        @if(outbound) {
            ?startNode ?linkProp ?node .

            FILTER NOT EXISTS {
                ?isPartOfProp rdfs:subPropertyOf* knora-base:isPartOf .
                ?startNode ?isPartOfProp ?node .
            }

            ?node knora-base:isDeleted false .

            ?linkValue a knora-base:LinkValue ;
                rdf:subject ?startNode ;
                rdf:predicate ?linkProp ;
                rdf:object ?node .
        } else {
            ?node ?linkProp ?startNode .

            FILTER NOT EXISTS {
                ?isPartOfProp rdfs:subPropertyOf* knora-base:isPartOf .
                ?node ?isPartOfProp ?startNode .
            }

            ?node knora-base:isDeleted false .

            ?linkValue a knora-base:LinkValue ;
                rdf:subject ?node ;
                rdf:predicate ?linkProp ;
                rdf:object ?startNode .
        }

        ?node a ?nodeClass ;
            rdfs:label ?nodeLabel ;
            knora-base:attachedToUser ?nodeCreator ;
            knora-base:attachedToProject ?nodeProject .

        OPTIONAL {
            ?node knora-base:hasPermissions ?nodePermissions .
        }

        ?linkValue knora-base:attachedToUser ?linkValueCreator .

        OPTIONAL {
            ?linkValue knora-base:hasPermissions ?linkValuePermissions .
        }
    }
}
LIMIT 50