@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and André Fatton.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi._
@import org.knora.webapi.twirl.SparqlTemplateLinkUpdate
@import org.knora.webapi.messages.v1respondermessages.valuemessages._

@**
 * Inserts a new value of a resource property (as opposed to a new version of an existing value).
 *
 * @param dataNamedGraph the named graph in which the project stores its data.
 * @param resourceIri the resource to update.
 * @param propertyIri the resource property to update.
 * @param newValueIri the new value IRI.
 * @param valueTypeIri the type of the new value.
 * @param permissionRelevantAssertions Permission-relevant assertions, i.e. owner and project plus permissions, in
 *        the form of a list of tuples (predicate, object).
 * @param linkUpdates a list of [[LinkUpdate]] objects describing links and LinkValues that need to be
 *        updated for resource references in Standoff text values.
 * @param maybeComment an optional comment on the value.
 * @param overrideCardinality if true, disregard the cardinality restrictions specified on the resource type.
 * @param value the value to insert.
 *
 * This operation will do nothing if the information submitted is inconsistent with the resource's class definition,
 * if the insertion would be redundant, or if the resource is nonexistent or marked as deleted. To find out whether
 * the update succeeded, the application must query the property's version history.
 *@
@(dataNamedGraph: IRI,
  resourceIri: IRI,
  propertyIri: IRI,
  newValueIri: IRI,
  valueTypeIri: IRI,
  value: UpdateValueV1,
  linkUpdates: Seq[SparqlTemplateLinkUpdate],
  maybeComment: Option[String],
  permissionRelevantAssertions: Seq[(IRI, IRI)],
  overrideCardinality: Boolean)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

DELETE {
    GRAPH ?dataNamedGraph {
        @* Delete the resource's last modification date so we can update it. *@
        ?resource knora-base:lastModificationDate ?resourceLastModificationDate .

        @* If any LinkValues need to be updated, detach them from the resource. *@
        @if(linkUpdates.exists(_.linkValueExists)) {
            @for((linkUpdate, linkValueIndex) <- linkUpdates.zipWithIndex) {
                @if(linkUpdate.linkValueExists) {
                    ?resource <@{linkUpdate.linkPropertyIri}Value> ?linkValue@linkValueIndex .
                }
            }
        }
    }
} INSERT {
    GRAPH ?dataNamedGraph {
        @* Construct the value. *@
        ?newValue rdf:type ?valueType .

        @value match {
            case textValue: TextValueV1 => {

                ?newValue knora-base:valueHasString """@textValue.utf8str""" .

                @* Create a Standoff node for each StandoffPositionV1. *@

                @for((attribute, position) <- textValue.flattenStandoff) {
                    ?newValue knora-base:valueHasStandoff
                        [
                            @*

                            If the StandoffPositionV1 contains both a resid and an href, we assume that the href is
                            redundant (i.e. it's based on the resid), so we ignore it.

                            *@
                            @(position.resid, position.href) match {
                                case (Some(resid), _) => {
                                    rdf:type knora-base:StandoffLink ;
                                    knora-base:standoffHasAttribute "@attribute" ;
                                    knora-base:standoffHasLink <@resid> ;
                                }

                                case (None, Some(href)) => {
                                    rdf:type knora-base:StandoffHref ;
                                    knora-base:standoffHasAttribute "@attribute" ;
                                    knora-base:standoffHasHref <@href> ;
                                }

                                case (None, None) => {
                                    rdf:type knora-base:StandoffVisualAttribute ;
                                    knora-base:standoffHasAttribute "@attribute" ;
                                }
                            }

                            knora-base:standoffHasStart @position.start ;
                            knora-base:standoffHasEnd @position.end
                        ] .
                }
            }

            case intValue: IntegerValueV1 => {

                ?newValue knora-base:valueHasInteger @intValue.ival .
                ?newValue knora-base:valueHasString """@intValue.ival.toString""" .

            }

            case floatValue: FloatValueV1 => {

                ?newValue knora-base:valueHasFloat @floatValue.fval .
                ?newValue knora-base:valueHasString """@floatValue.fval.toString""" .

            }

            case jdcValue: JulianDayCountValueV1 => {

                ?newValue knora-base:valueHasStartJDC @jdcValue.dateval1 .
                ?newValue knora-base:valueHasEndJDC @jdcValue.dateval2 .
                ?newValue knora-base:valueHasStartPrecision "@jdcValue.dateprecision1" .
                ?newValue knora-base:valueHasEndPrecision "@jdcValue.dateprecision2" .
                ?newValue knora-base:valueHasCalendar "@jdcValue.calendar" .
                ?newValue knora-base:valueHasString """@jdcValue.toString""" .

            }

            case colorValue: ColorValueV1 => {

                ?newValue knora-base:valueHasColor """@colorValue.color""" .
                ?newValue knora-base:valueHasString """@colorValue.toString""" .

            }

            case geometryValue: GeomValueV1 => {

                ?newValue knora-base:valueHasGeometry """@geometryValue.geom""" .
                ?newValue knora-base:valueHasString """@geometryValue.toString""" .

            }

            case stillImageFileValue: StillImageFileValueV1 => {
                ?newValue knora-base:originalFilename """@stillImageFileValue.originalFilename""" .
                ?newValue knora-base:originalMimeType """@stillImageFileValue.originalMimeType""" .
                ?newValue knora-base:internalFilename """@stillImageFileValue.internalFilename""" .
                ?newValue knora-base:internalMimeType """@stillImageFileValue.internalMimeType""" .
                ?newValue knora-base:dimX @stillImageFileValue.dimX .
                ?newValue knora-base:dimY @stillImageFileValue.dimY .
                ?newValue knora-base:qualityLevel @stillImageFileValue.qualityLevel .
                ?newValue knora-base:valueHasQname """@stillImageFileValue.qualityName""" .

                @if(stillImageFileValue.isPreview) {
                    ?newValue knora-base:valueIsPreview true .
                }

                ?newValue knora-base:valueHasString """@stillImageFileValue.toString""" .
            }

            case other => {
                @{throw SparqlGenerationException(s"Value object $other is not supported in this SPARQL template"); ()}
            }
        }

        @* TODO: add remaining types. *@

        @* Insert the value's comment, if given. *@
        @maybeComment match {
            case Some(comment) => {
                ?newValue knora-base:valueHasComment """@comment""" .
            }

            case None => {}
        }

        @* Insert the value's permission-relevant assertions (its owner, project, and permissions). *@
        @for(assertion <- permissionRelevantAssertions) {
            ?newValue <@assertion._1> <@assertion._2> .
        }

        ?newValue knora-base:valueHasOrder ?nextOrder .
        ?newValue knora-base:valueCreationDate ?currentTime .

        @* Insert direct links and LinkValues for resource references. *@

        @for((linkUpdate, linkValueIndex) <- linkUpdates.zipWithIndex) {
            @* Insert a direct link for the resource reference if necessary. *@
            @if(linkUpdate.insertDirectLink) {
                ?resource <@linkUpdate.linkPropertyIri> <@linkUpdate.linkTargetIri> .
            }

            @* Insert a LinkValue for the resource reference. *@
            <@linkUpdate.newLinkValueIri> rdf:type knora-base:LinkValue ;
                rdf:subject ?resource ;
                rdf:predicate <@linkUpdate.linkPropertyIri> ;
                rdf:object <@linkUpdate.linkTargetIri> ;
                knora-base:valueHasRefCount @linkUpdate.newReferenceCount ;
                knora-base:valueCreationDate ?currentTime .

            @for(assertion <- linkUpdate.permissionRelevantAssertions) {
                <@linkUpdate.newLinkValueIri> <@assertion._1> <@assertion._2> .
            }

            @* If we're inserting a new version of an existing LinkValue, attach it to the previous version. *@
            @if(linkUpdate.linkValueExists) {
                <@linkUpdate.newLinkValueIri> knora-base:previousValue ?linkValue@linkValueIndex .
            }

            @* Attach the new LinkValue to its containing resource. *@
            ?resource <@{linkUpdate.linkPropertyIri}Value> <@linkUpdate.newLinkValueIri> .
        }

        @* Attach the value to the resource. *@
        ?resource ?property ?newValue .

        @* Update the resource's last modification date. *@
        ?resource knora-base:lastModificationDate ?currentTime .
    }
} WHERE {
    BIND(IRI("@dataNamedGraph") AS ?dataNamedGraph)
    BIND(IRI("@resourceIri") AS ?resource)
    BIND(IRI("@propertyIri") AS ?property)
    BIND(IRI("@newValueIri") AS ?newValue)
    BIND(IRI("@valueTypeIri") AS ?valueType)
    BIND(NOW() AS ?currentTime)

    @* Do nothing if the resource doesn't exist, is marked as deleted, or isn't actually a resource. *@

    ?resource rdf:type ?resourceClass .
    ?resourceClass rdfs:subClassOf+ knora-base:Resource .

    MINUS {
        ?resource knora-base:isDeleted true .
    }

    @* Do nothing if the submitted value has the wrong type. *@

	?property rdfs:range ?propertyRange .
    ?valueType rdfs:subClassOf* ?propertyRange .

    @if(!overrideCardinality) {

        @* Do nothing if neither the resource class nor any of its superclasses has a cardinality for this property. *@

        ?resourceClass rdfs:subClassOf* ?restriction .
        ?restriction a owl:Restriction .
        ?restriction owl:onProperty ?property .

        @*

        Do nothing if the resource class is allowed to have at most one value for this property, and it already has one,
        unless the value is marked as deleted. TODO: This is commented out because it's slow on Jena/Fuseki.

        MINUS {
            ?restriction owl:cardinality 1 .
            ?resource ?property ?value .

            MINUS {
                ?value knora-base:isDeleted true .
            }
        }

        MINUS {
            ?restriction owl:maxCardinality 1 .
            ?resource ?property ?value .

            MINUS {
                ?value knora-base:isDeleted true .
            }
        }

        MINUS {
            ?restriction owl:qualifiedCardinality 1 .
            ?resource ?property ?value .

            MINUS {
                ?value knora-base:isDeleted true .
            }
        }

        MINUS {
            ?restriction owl:maxQualifiedCardinality 1 .
            ?resource ?property ?value .

            MINUS {
                ?value knora-base:isDeleted true .
            }
        }

        *@
    }

    @*

    Check the state of any LinkValues to be updated for resource references.

    *@

    @for((linkUpdate, linkValueIndex) <- linkUpdates.zipWithIndex) {
        @if(linkUpdate.insertDirectLink) {
            @*

            Do nothing if we were asked to insert a direct link to a target that doesn't exist, is marked as deleted,
            isn't a knora-base:Resource, or belongs to the wrong OWL class for the link property.

            *@

            <@linkUpdate.linkTargetIri> rdf:type ?linkTargetClass .

            MINUS {
                <@linkUpdate.linkTargetIri> knora-base:isDeleted true .
            }

            ?linkTargetClass rdfs:subClassOf+ knora-base:Resource .
            <@linkUpdate.linkPropertyIri> rdfs:range ?expectedTargetClass .
            ?linkTargetClass rdfs:subClassOf* ?expectedTargetClass .
        }

        @if(linkUpdate.directLinkExists) {
            @* Make sure the relevant direct link exists between the two resources. *@
            ?resource <@linkUpdate.linkPropertyIri> <@linkUpdate.linkTargetIri> .
        } else {
            @* Make sure there no such direct link. *@
            MINUS {
                ?resource <@linkUpdate.linkPropertyIri> <@linkUpdate.linkTargetIri> .
            }
        }

        @if(linkUpdate.linkValueExists) {
            @*

            Make sure a knora-base:LinkValue exists, describing the relevant direct link
            between the source and target resources, and has the correct reference count.

            *@
            ?resource <@{linkUpdate.linkPropertyIri}Value> ?linkValue@linkValueIndex .
            ?linkValue@linkValueIndex rdf:type knora-base:LinkValue ;
                rdf:subject ?resource ;
                rdf:predicate <@linkUpdate.linkPropertyIri> ;
                rdf:object <@linkUpdate.linkTargetIri> ;
                knora-base:valueHasRefCount @linkUpdate.currentReferenceCount .

            MINUS {
                ?linkValue@linkValueIndex knora-base:isDeleted true .
            }
        } else {
            @* Make sure there is no such knora-base:LinkValue. *@
            MINUS {
                ?resource <@{linkUpdate.linkPropertyIri}Value> ?linkValue@linkValueIndex .
                ?linkValue@linkValueIndex rdf:type knora-base:LinkValue ;
                    rdf:subject ?resource ;
                    rdf:predicate <@linkUpdate.linkPropertyIri> ;
                    rdf:object <@linkUpdate.linkTargetIri> .

                MINUS {
                    ?linkValue@linkValueIndex knora-base:isDeleted true .
                }
            }
        }
    }

    @* Get the resource's last modification date, if it has one, so we can update it. *@

    OPTIONAL {
        ?resource knora-base:lastModificationDate ?resourceLastModificationDate .
    }

    @*

    Generate knora-base:valueHasOrder for the new value. If the property already has one or more (non-deleted)
    values in the resource, find the one with the highest order, add 1 to its order, and give the resulting
    order to the new value. Otherwise, give the new value an order of 0.

    *@

    {
        SELECT ((MAX(?order)) AS ?maxOrder) (IF(BOUND(?maxOrder), ?maxOrder + 1, 0) AS ?nextOrder)
        WHERE {
            BIND(IRI("@resourceIri") AS ?resource)
            BIND(IRI("@propertyIri") AS ?property)

            ?resource ?property ?value .
            ?value knora-base:valueHasOrder ?order .

            MINUS {
                ?value knora-base:isDeleted true .
            }
        }
    }
}

