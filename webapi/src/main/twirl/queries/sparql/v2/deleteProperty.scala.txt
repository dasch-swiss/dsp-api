@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi._
@import org.knora.webapi.messages.SmartIri
@import org.knora.webapi.messages.v2.responder.ontologymessages._
@import java.time.Instant

@*
 * Deletes a property.
 *
 * @param triplestore the name of the triplestore being used.
 * @param ontologyNamedGraphIri the IRI of the named graph where the ontology is stored.
 * @param ontologyIri the IRI of the ontology containing the property.
 * @param propertyIri the IRI of the property to be deleted.
 * @param maybeLinkValuePropertyIri if propertyIri refers to a link property, the IRI of the corresponding link value
 *        property, which will also be deleted.
 * @param lastModificationDate the xsd:dateTimeStamp that was attached to the ontology when it was last modified.
 * @param currentTime an xsd:dateTimeStamp that will be attached to the ontology.
 *@
@(triplestore: String,
  ontologyNamedGraphIri: SmartIri,
  ontologyIri: SmartIri,
  propertyIri: SmartIri,
  maybeLinkValuePropertyIri: Option[SmartIri],
  lastModificationDate: Instant,
  currentTime: Instant)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

DELETE {
    GRAPH ?ontologyNamedGraph {
        ?ontology knora-base:lastModificationDate "@lastModificationDate"^^xsd:dateTime .
        ?property ?propertyPred ?propertyObj .

        @maybeLinkValuePropertyIri match {

            case Some(linkValuePropertyIri) => {

                <@linkValuePropertyIri> ?linkValuePropertyPred ?linkValuePropertyObj .

            }

            case None => {}

        }
    }
} INSERT {
    GRAPH ?ontologyNamedGraph {
        ?ontology knora-base:lastModificationDate "@currentTime"^^xsd:dateTime .
    }
}
@* Ensure that inference is not used in the WHERE clause of this update. *@
@if(triplestore.startsWith("graphdb")) {
    USING <http://www.ontotext.com/explicit>
}
WHERE {
    BIND(IRI("@ontologyNamedGraphIri") AS ?ontologyNamedGraph)
    BIND(IRI("@ontologyIri") AS ?ontology)
    BIND(IRI("@propertyIri") AS ?property)

    GRAPH ?ontologyNamedGraph {
        ?ontology rdf:type owl:Ontology ;
            knora-base:lastModificationDate "@lastModificationDate"^^xsd:dateTime .

        ?property rdf:type owl:ObjectProperty ;
            ?propertyPred ?propertyObj .

        FILTER NOT EXISTS {
            ?s ?p ?property .
        }

        @maybeLinkValuePropertyIri match {

            case Some(linkValuePropertyIri) => {

                <@linkValuePropertyIri> rdf:type owl:ObjectProperty ;
                    ?linkValuePropertyPred ?linkValuePropertyObj .

                FILTER NOT EXISTS {
                    ?q ?r <@linkValuePropertyIri> .
                }

            }

            case None => {}

        }
    }
}
