@*
 * Copyright Â© 2015-2018 the contributors (see Contributors.md).
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI
@import org.knora.webapi.KnoraSystemInstances

@**
 * Updates an existing user with the provided values.
 *
 * @param dataNamedGraph     the named graph to update.
 * @param triplestore        the name of the triplestore being used. The template uses this value to exclude inferred
                             results from the WHERE clause of the update.
 * @param userIri            the IRI of the user we want to update.
 * @param maybeUsername      the new optional value for username.
 * @param maybeEmail         the new optional value for email.
 * @param maybeGivenName     the new optional value for given name.
 * @param maybeFamilyName    the new optional value for family name.
 * @param maybePassword      the new optional value for value for password.
 * @param maybeStatus        the new optional value for status.
 * @param maybeLang          the new optional value for lang.
 * @param maybeProjects      the new optional value for projects.
 * @param maybeProjectsAdmin the new optional value for project admin.
 * @param maybeGroups        the new optional value for groups.
 * @param maybeSystemAdmin   the new optional value for system admin.
 *@
@(adminNamedGraphIri: IRI,
  triplestore: String,
  userIri: IRI,
  maybeUsername: Option[String],
  maybeEmail: Option[String],
  maybeGivenName: Option[String],
  maybeFamilyName: Option[String],
  maybePassword: Option[String],
  maybeStatus: Option[Boolean],
  maybeLang: Option[String],
  maybeProjects: Option[Seq[IRI]],
  maybeProjectsAdmin: Option[Seq[IRI]],
  maybeGroups: Option[Seq[IRI]],
  maybeSystemAdmin: Option[Boolean])

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

WITH <@adminNamedGraphIri>
DELETE {

    @* Delete current values, for which we have a new one. *@

    @if(maybeUsername.nonEmpty) {
        ?user knora-base:username ?currentUsername .
    }

    @if(maybeEmail.nonEmpty) {
        ?user knora-base:email ?currentEmail .
    }

    @if(maybeGivenName.nonEmpty) {
        ?user knora-base:givenName ?currentGivenName .
    }

    @if(maybeFamilyName.nonEmpty) {
        ?user knora-base:familyName ?currentFamilyName .
    }

    @if(maybePassword.nonEmpty) {
        ?user knora-base:password ?currentPassword .
    }

    @if(maybeStatus.nonEmpty) {
        ?user knora-base:status ?currentStatus .
    }

    @if(maybeLang.nonEmpty) {
        ?user knora-base:preferredLanguage ?currentPreferredLanguage .
    }

    @if(maybeProjects.nonEmpty) {
        ?user knora-base:isInProject ?currentProjects .
    }

    @if(maybeProjectsAdmin.nonEmpty) {
        ?user knora-base:isInProjectAdminGroup ?currentProjectsAdmin .
    }

    @if(maybeGroups.nonEmpty) {
        ?user knora-base:isInGroup ?currentGroups .
    }

    @if(maybeSystemAdmin.nonEmpty) {
        ?user knora-base:isInSystemAdminGroup ?currentIsInSystemAdminGroup .
    }

} INSERT {

    @* Add the new values. *@

    @if(maybeUsername.nonEmpty) {
        ?user knora-base:username "@maybeUsername.get"^^xsd:string .
    }

    @if(maybeEmail.nonEmpty) {
        ?user knora-base:email "@maybeEmail.get"^^xsd:string .
    }

    @if(maybeGivenName.nonEmpty) {
        ?user knora-base:givenName "@maybeGivenName.get"^^xsd:string .
    }

    @if(maybeFamilyName.nonEmpty) {
        ?user knora-base:familyName "@maybeFamilyName.get"^^xsd:string .
    }

    @if(maybePassword.nonEmpty) {
        ?user knora-base:password "@maybePassword.get"^^xsd:string .
    }

    @if(maybeStatus.nonEmpty) {
        ?user knora-base:status "@maybeStatus.get"^^xsd:boolean .
    }

    @if(maybeLang.nonEmpty) {
        ?user knora-base:preferredLanguage "@maybeLang.get"^^xsd:string .
    }

    @if(maybeProjects.nonEmpty) {
        @for(project <- maybeProjects.get) {
          ?user knora-base:isInProject <@project> .
        }
    }

    @if(maybeProjectsAdmin.nonEmpty) {
        @for(project <- maybeProjectsAdmin.get) {
          ?user knora-base:isInProjectAdminGroup <@project> .
        }
    }

    @if(maybeGroups.nonEmpty) {
        @for(group <- maybeGroups.get) {
          ?user knora-base:isInGroup <@group> .
        }
    }

    @if(maybeSystemAdmin.nonEmpty) {
        ?user knora-base:isInSystemAdminGroup "@maybeSystemAdmin.get"^^xsd:boolean .
    }

}

@*

GraphDB's consistency checking requires reasoning, but reasoning interferes with certain things
in the WHERE clauses of our SPARQL updates, so we set a GraphDB-specific flag to return only
explicit statements in the WHERE clause here.

*@

@triplestore match {
    case "graphdb" | "graphdb-free" => {
        USING <http://www.ontotext.com/explicit>
    }

    case other => {}
}

WHERE {
    BIND(IRI("@userIri") AS ?user)

    @* Get all current defined values. *@

    ?user knora-base:username ?currentUsername .

    ?user knora-base:email ?currentEmail .

    ?user knora-base:givenName ?currentGivenName .

    ?user knora-base:familyName ?currentFamilyName .

    ?user knora-base:password ?currentPassword .

    ?user knora-base:status ?currentStatus .

    ?user knora-base:preferredLanguage ?currentPreferredLanguage .

    optional {?user knora-base:isInProject ?currentProjects .}

    optional {?user knora-base:isInProjectAdminGroup ?currentProjectsAdmin .}

    optional {?user knora-base:isInGroup ?currentGroups .}

    ?user knora-base:isInSystemAdminGroup ?currentIsInSystemAdminGroup .

    FILTER(!(?user = IRI("@KnoraSystemInstances.Users.AnonymousUser.id") || ?s = IRI("@KnoraSystemInstances.Users.SystemUser.id")))
}
