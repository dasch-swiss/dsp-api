@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@**
 * Given a node in a hierarchical list, returns the nodes in the path from the root node to the specified node,
 * not including the root node.
 *
 * @param triplestore the name of the triplestore being used.
 * @param queryNodeIri the node whose path is to be queried.
 * @param preferredLanguage the language code of the user's preferred language.
 * @param fallbackLanguage the language code of the application's default language.
 *@
@(triplestore: String,
  queryNodeIri: IRI,
  preferredLanguage: String,
  fallbackLanguage: String)

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT DISTINCT ?node ?nodeName ?label ?child
@* Ensure that inference is not used in this query. *@
@if(triplestore.startsWith("graphdb")) {
    FROM <http://www.ontotext.com/explicit>
}
WHERE {
    BIND(IRI("@queryNodeIri") as ?leafNode)
    BIND(str("@preferredLanguage") as ?preferredLanguage)
    BIND(str("@fallbackLanguage") as ?fallbackLanguage)

    ?node knora-base:hasSubListNode* ?leafNode .

    MINUS {
        ?node knora-base:isRootNode "true"^^xsd:boolean .
    }

    OPTIONAL {
        ?node knora-base:hasSubListNode ?child .
        ?child knora-base:hasSubListNode* ?leafNode .
    }

    OPTIONAL {
        ?node knora-base:listNodeName ?nodeName .
    }

    OPTIONAL {
        ?node rdfs:label ?preferredLanguageLabel .
        FILTER (LANG(?preferredLanguageLabel) = ?preferredLanguage) .
    }

    OPTIONAL {
        ?node rdfs:label ?fallbackLanguageLabel .
        FILTER (LANG(?fallbackLanguageLabel) = ?fallbackLanguage) .
    }

    OPTIONAL {
        ?node rdfs:label ?anyLanguageLabel .
    }

    BIND(COALESCE(str(?preferredLanguageLabel), str(?fallbackLanguageLabel), str(?anyLanguageLabel)) AS ?label)
}
