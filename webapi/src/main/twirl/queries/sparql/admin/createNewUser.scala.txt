@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@*
 * Creates a new user.
 *
 * @param triplestore the name of the triplestore being used. The template uses this value to exclude inferred
                      results from the WHERE clause of the update.
 * @param userIri the Iri of the new user
 * @param userClassIri the IRI of the OWL class that the new user should belong to.
 * @param username the new user's username.
 * @param email the new user's email address.
 * @param password the new user's password.
 * @param givenName the new user's given name.
 * @param familyName the new user's family name.
 * @param status the new user's status (active = true, inactive = false)
 * @param preferredLanguage the new user's preferred language.
 * @param systemAdmin the new user's system admin group membership.
 *
 *@
@(adminNamedGraphIri: IRI,
  triplestore: String,
  userIri: IRI,
  userClassIri: IRI,
  username: String,
  email: String,
  password: String,
  givenName: String,
  familyName: String,
  status: Boolean,
  preferredLanguage: String,
  systemAdmin: Boolean)

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX knora-admin: <http://www.knora.org/ontology/knora-admin#>

INSERT {
    GRAPH ?adminNamedGraphIri {
        ?userIri rdf:type ?userClassIri .

        ?userIri knora-admin:username "@username"^^xsd:string .

        ?userIri knora-admin:email "@email"^^xsd:string .

        ?userIri knora-admin:password "@password"^^xsd:string .

        ?userIri knora-admin:givenName "@givenName"^^xsd:string .

        ?userIri knora-admin:familyName "@familyName"^^xsd:string .

        ?userIri knora-admin:status "@status"^^xsd:boolean .

        ?userIri knora-admin:preferredLanguage "@preferredLanguage"^^xsd:string .

        ?userIri knora-admin:isInSystemAdminGroup "@systemAdmin"^^xsd:boolean .
    }
}
@*

GraphDB's consistency checking requires reasoning, but reasoning interferes with certain things
in the WHERE clauses of our SPARQL updates, so we set a GraphDB-specific flag to return only
explicit statements in the WHERE clause here.

*@
@triplestore match {
    case "graphdb" | "graphdb-free" => {
        USING <http://www.ontotext.com/explicit>
    }

    case other => {}
}
WHERE {
    BIND(IRI("@adminNamedGraphIri") AS ?adminNamedGraphIri)
    BIND(IRI("@userIri") AS ?userIri)
    BIND(IRI("@userClassIri") AS ?userClassIri)

    FILTER NOT EXISTS { ?s knora-admin:username "@username"^^xsd:string . }
    FILTER NOT EXISTS { ?s knora-admin:email "@email"^^xsd:string . }
}
