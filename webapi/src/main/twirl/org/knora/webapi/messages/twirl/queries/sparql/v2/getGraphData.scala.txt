@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi._
@import org.knora.webapi.messages.SmartIri

@**
 * Gets the outbound or inbound links from/to a resource using SPARQL w/o inference. This query is used
 * recursively to get a graph of resources reachable from a given resource.
 *
 * @param startNodeIri the IRI of the resource to use as the starting point of the query.
 * @param startNodeOnly if true, returns information only about the start node.
 * @param maybeExcludeLinkProperty if provided, a link property that should be excluded from the results.
 * @param outbound true to get outbound links, false to get inbound links.
 * @param limit the maximum number of edges to return.
 *@
@(startNodeIri: IRI,
  startNodeOnly: Boolean,
  maybeExcludeLinkProperty: Option[SmartIri],
  outbound: Boolean,
  limit: Int)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?node ?nodeClass ?nodeLabel ?nodeCreator ?nodeProject ?nodePermissions
       ?linkValue ?linkProp ?linkValueCreator ?linkValuePermissions
WHERE {
    @if(startNodeOnly) {
        BIND(IRI("@startNodeIri") AS ?node) .

        ?node a ?nodeClass ;
            rdfs:label ?nodeLabel ;
            knora-base:attachedToUser ?nodeCreator ;
            knora-base:attachedToProject ?nodeProject ;
            knora-base:hasPermissions ?nodePermissions .

        FILTER NOT EXISTS {
            ?node knora-base:isDeleted true .
        }
    } else {
        BIND(IRI("@startNodeIri") AS ?startNode) .

        ?linkProp rdfs:subPropertyOf* knora-base:hasLinkTo .

        @if(outbound) {
            ?startNode ?linkProp ?node .

            @maybeExcludeLinkProperty match {
                case Some(excludeLinkProperty) => {

                    FILTER NOT EXISTS {
                        ?excludedProp rdfs:subPropertyOf* <@excludeLinkProperty> .
                        ?startNode ?excludedProp ?node .
                    }

                }

                case None => {}
            }

            FILTER NOT EXISTS {
                ?node knora-base:isDeleted true .
            }

            ?linkValue a knora-base:LinkValue ;
                rdf:subject ?startNode ;
                rdf:predicate ?linkProp ;
                rdf:object ?node .
        } else {
            ?node ?linkProp ?startNode .

            @maybeExcludeLinkProperty match {
                case Some(excludeLinkProperty) => {

                    FILTER NOT EXISTS {
                        ?excludedProp rdfs:subPropertyOf* <@excludeLinkProperty> .
                        ?node ?excludedProp ?startNode .
                    }

                }

                case None => {}
            }

            FILTER NOT EXISTS {
                ?node knora-base:isDeleted true .
            }

            ?linkValue a knora-base:LinkValue ;
                rdf:subject ?node ;
                rdf:predicate ?linkProp ;
                rdf:object ?startNode .
        }

        ?node a ?nodeClass ;
            rdfs:label ?nodeLabel ;
            knora-base:attachedToUser ?nodeCreator ;
            knora-base:attachedToProject ?nodeProject ;
            knora-base:hasPermissions ?nodePermissions .

        ?linkValue knora-base:attachedToUser ?linkValueCreator ;
            knora-base:hasPermissions ?linkValuePermissions .
    }
}
LIMIT @limit
