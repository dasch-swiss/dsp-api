@*
 * Copyright Â© 2015-2021 the contributors (see Contributors.md).
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import java.time.Instant
@import org.knora.webapi._

@**
 * Erases a resource from the triplestore using GraphDB.
 *
 * @param dataNamedGraph the named graph to update.
 * @param resourceIri the IRI of the resource to be marked as deleted.
 *@
@(dataNamedGraph: IRI,
  resourceIri: IRI)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

DELETE {
    GRAPH ?dataNamedGraph {
        <@resourceIri> ?resourcePred ?resourceObj .
        ?value ?valuePred ?valueObj .
        ?standoff ?standoffPred ?standoffObj .
        ?otherResource ?otherResourceProp ?otherResourceCurrentLinkValue .
        ?otherResourceLinkValue ?otherResourceLinkValuePred ?otherResourceLinkValueObj .
    }
} WHERE {
    BIND(IRI("@dataNamedGraph") AS ?dataNamedGraph)

    @* Make sure the resource is really a resource. *@
    <@resourceIri> a knora-base:Resource .

    {
        @* Collect all statements whose subject is the resource. *@

        GRAPH <http://www.ontotext.com/explicit> {
            <@resourceIri> ?resourcePred ?resourceObj .
        }
    } UNION {
        @* Collect all statements whose subject is a value of the resource. *@

        GRAPH <http://www.ontotext.com/explicit> {
            <@resourceIri> ?valueProp ?currentValue .
        }

        ?currentValue a knora-base:Value .

        GRAPH <http://www.ontotext.com/explicit> {
            ?currentValue knora-base:previousValue* ?value .
            ?value ?valuePred ?valueObj .
        }
    } UNION {
        @* Collect all statements whose subject is a standoff tag attached to a value of the resource. *@

        GRAPH <http://www.ontotext.com/explicit> {
            <@resourceIri> ?valueProp ?currentTextValue .
            ?currentTextValue a knora-base:TextValue ;
                knora-base:previousValue* ?textValue .
            ?textValue knora-base:valueHasStandoff ?standoff .
            ?standoff ?standoffPred ?standoffObj .
        }
    } UNION {
        @* Collect all statements about marked-as-deleted link values that point to the resource. *@

        GRAPH <http://www.ontotext.com/explicit> {
            ?otherResourceCurrentLinkValue rdf:object <@resourceIri> ;
                knora-base:isDeleted true ;
                a knora-base:LinkValue ;
                knora-base:previousValue* ?otherResourceLinkValue .
            ?otherResourceLinkValue ?otherResourceLinkValuePred ?otherResourceLinkValueObj .
            ?otherResource ?otherResourceProp ?otherResourceCurrentLinkValue .
        }
    }
}
