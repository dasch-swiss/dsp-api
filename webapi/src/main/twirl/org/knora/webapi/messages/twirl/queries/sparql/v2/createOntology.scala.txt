@*
 * Copyright Â© 2021 - 2024 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.messages.SmartIri
@import org.knora.webapi.slice.admin.domain.model.KnoraProject.ProjectIri
@import java.time.Instant
@import eu.timepit.refined.types.string.NonEmptyString

@*
 * Creates a new, empty ontology. If the ontology already exists, this update does nothing.
 *
 * @param ontologyNamedGraphIri the IRI of the named graph where the ontology should be stored.
 * @param ontologyIri the IRI of the ontology to be created.
 * @param projectIri the IRI of the project that the ontology belongs to.
 * @param currentTime a xsd:dateTimeStamp that will be attached to the ontology.
 *@
@(ontologyNamedGraphIri: SmartIri,
  ontologyIri: SmartIri,
  projectIri: ProjectIri,
  isShared: Boolean,
  ontologyLabel: String,
  ontologyComment: Option[NonEmptyString],
  currentTime: Instant)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

INSERT {
    GRAPH ?ontologyNamedGraph {
        ?ontology rdf:type owl:Ontology ;
            knora-base:attachedToProject ?project ;
            knora-base:isShared @isShared ;
            rdfs:label """@ontologyLabel"""^^xsd:string ;
            @if(ontologyComment.nonEmpty) {
                rdfs:comment """@ontologyComment.get.value"""^^xsd:string ;
            }
            knora-base:lastModificationDate "@currentTime"^^xsd:dateTime .
    }
}

WHERE {
    BIND(IRI("@ontologyNamedGraphIri") AS ?ontologyNamedGraph)
    BIND(IRI("@ontologyIri") AS ?ontology)
    BIND(IRI("@projectIri.value") AS ?project)

    FILTER NOT EXISTS {
        ?ontology rdf:type ?existingOntologyType .
    }
}
