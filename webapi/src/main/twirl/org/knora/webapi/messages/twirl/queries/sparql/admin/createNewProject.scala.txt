@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI
@import dsp.valueobjects.V2

@*
 * Creates a new project.
 *
 * @param adminNamedGraphIri the name of the graph into which the new project will be created.
 * @param projectIri the IRI of the new project
 * @param projectClassIri the IRI of the OWL class that the new project should belong to.
 * @param shortname the new project's shortname (required / unique).
 * @param maybeShortcode the new project's shortcode (optional / unique)
 * @param maybeLongname the new project's longname.
 * @param maybeDescription the new project's description.
 * @param maybeKeywords the new project's keywords.
 * @param maybeLogo the new project's logo.
 * @param status the new project's status.
 * @param hasSelfJoinEnabled the new project's self join status.
 *
 *@
@(adminNamedGraphIri: IRI,
  projectIri: IRI,
  projectClassIri: IRI,
  shortname: String,
  shortcode: String,
  maybeLongname: Option[String],
  maybeDescriptions: Option[Seq[V2.StringLiteralV2]],
  maybeKeywords: Option[Seq[String]],
  maybeLogo: Option[String],
  status: Boolean,
  hasSelfJoinEnabled: Boolean)

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX knora-admin: <http://www.knora.org/ontology/knora-admin#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT {
    GRAPH ?adminNamedGraphIri {
        ?projectIri rdf:type ?projectClassIri ;

            knora-admin:projectShortname "@shortname"^^xsd:string ;

            knora-admin:projectShortcode "@shortcode"^^xsd:string ;

            @if(maybeLongname.nonEmpty) {
                knora-admin:projectLongname """@maybeLongname.get"""^^xsd:string ;
            }

            @if(maybeDescriptions.nonEmpty) {
                @for(description <- maybeDescriptions.get) {
                    @if(description.language.nonEmpty) {
                        knora-admin:projectDescription """@description.value"""@@@{description.language.get} ;
                    } else {
                        knora-admin:projectDescription """@description.value"""^^xsd:string ;
                    }
                }
            }

            @if(maybeKeywords.nonEmpty) {
                @for(keyword <- maybeKeywords.get) {
                  knora-admin:projectKeyword "@keyword"^^xsd:string ;
                }
            }

            @if(maybeLogo.nonEmpty) {
                knora-admin:projectLogo "@maybeLogo.get"^^xsd:string ;
            }

            knora-admin:status "@status"^^xsd:boolean ;

            knora-admin:hasSelfJoinEnabled "@hasSelfJoinEnabled"^^xsd:boolean .
    }
}

WHERE {
    BIND(IRI("@adminNamedGraphIri") AS ?adminNamedGraphIri)
    BIND(IRI("@projectIri") AS ?projectIri)
    BIND(IRI("@projectClassIri") AS ?projectClassIri)

    FILTER NOT EXISTS { ?s knora-admin:projectShortname "@shortname"^^xsd:string . }

    FILTER NOT EXISTS { ?s knora-admin:projectShortcode "@shortcode"^^xsd:string . }
}
