@*
 * Copyright Â© 2021 - 2023 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI
@import org.knora.webapi.messages.util.KnoraSystemInstances

@**
 * Gets all admin members of a project, given the project's IRI, shortname, and/or shortcode.
 *
 * @param maybeIri the project's IRI.
 * @param maybeShortname the project's shortname.
 * @param maybeShortcode the project's shortcode.
 *@
@(maybeIri: Option[IRI],
  maybeShortname: Option[String],
  maybeShortcode: Option[String])

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX knora-admin: <http://www.knora.org/ontology/knora-admin#>

CONSTRUCT {?s knora-admin:isInProjectAdminGroup ?project . }

WHERE {

    @if(maybeIri.nonEmpty) {
        BIND(IRI("@maybeIri") as ?project)
    }

    @if(maybeShortname.nonEmpty) {
        ?project knora-admin:projectShortname "@maybeShortname.get"^^xsd:string .
    }

    @if(maybeShortcode.nonEmpty) {
        ?project knora-admin:projectShortcode "@maybeShortcode.get"^^xsd:string .
    }

    ?project rdf:type knora-admin:knoraProject .

    ?s knora-admin:isInProjectAdminGroup ?project .

    ?s rdf:type knora-admin:User .

    FILTER(!(?s = IRI("@KnoraSystemInstances.Users.AnonymousUser.id") || ?s = IRI("@KnoraSystemInstances.Users.SystemUser.id")))
}
