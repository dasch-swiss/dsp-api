@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.messages.SmartIri

@*
 * Gets a resource class definition.
 *
 * @param triplestore the name of the triplestore being used.
 * @param classIri the IRI of the class.
 *@
@(triplestore: String,
  classIri: SmartIri)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

CONSTRUCT {
    ?class ?classPred ?classObj ;
        rdfs:subClassOf ?bnode .

    ?bnode rdf:type owl:Restriction ;
        ?cardinalityPred ?cardinalityValue .
}

WHERE {
    BIND(IRI("@classIri") AS ?class)

    ?class rdf:type owl:Class .

    {
        ?class ?classPred ?classObj .
    } UNION {
        ?class rdfs:subClassOf ?bnode .
        ?bnode rdf:type owl:Restriction ;
            ?cardinalityPred ?cardinalityValue .
    }
}
