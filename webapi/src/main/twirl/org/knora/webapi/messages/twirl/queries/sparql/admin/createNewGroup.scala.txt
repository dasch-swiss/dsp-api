@*
 * Copyright Â© 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI
@import org.knora.webapi.messages.store.triplestoremessages.StringLiteralV2

@*
 * Creates a new group.
 *
 * @param adminNamedGraphIri the IRI of the admin graph.
 * @param triplestore the name of the triplestore being used. The template uses this value to exclude inferred
                      results from the WHERE clause of the update.
 * @param groupIri the Iri of the new group
 * @param groupClassIri the IRI of the OWL class that the new group should belong to.
 * @param name the new group's name.
 * @param descriptions the new group's descriptions.
 * @param projectIri the new group's project association.
 * @param isActiveGroup the new group's status.
 * @param hasSelfJoinEnabled the new group's self-join status.
 *@
@(adminNamedGraphIri: IRI,
  triplestore: String,
  groupIri: IRI,
  groupClassIri: IRI,
  name: String,
  descriptions: Seq[StringLiteralV2],
  projectIri: IRI,
  status: Boolean,
  hasSelfJoinEnabled: Boolean)

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX knora-admin: <http://www.knora.org/ontology/knora-admin#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT {
    GRAPH ?adminNamedGraphIri {
        ?groupIri rdf:type ?groupClassIri .

            ?groupIri knora-admin:groupName "@name"^^xsd:string .

            @if(descriptions.nonEmpty) {
                @for(d <- descriptions) {
                    @if(d.language.nonEmpty) {
                        ?groupIri knora-admin:groupDescriptions """@d.value"""@@@{d.language.get} .
                    } else {
                        ?groupIri knora-admin:groupDescriptions """@d.value"""^^xsd:string .
                    }
                }
            }

            ?groupIri knora-admin:belongsToProject ?projectIri .

            ?groupIri knora-admin:status "@status"^^xsd:boolean .

            ?groupIri knora-admin:hasSelfJoinEnabled "@hasSelfJoinEnabled"^^xsd:boolean .
    }
}
@*

GraphDB's consistency checking requires reasoning, but reasoning interferes with certain things
in the WHERE clauses of our SPARQL updates, so we set a GraphDB-specific flag to return only
explicit statements in the WHERE clause here.

*@
@triplestore match {
    case "graphdb" | "graphdb-free" => {
        USING <http://www.ontotext.com/explicit>
    }

    case other => {}
}
WHERE {
    BIND(IRI("@adminNamedGraphIri") AS ?adminNamedGraphIri)
    BIND(IRI("@groupIri") AS ?groupIri)
    BIND(IRI("@groupClassIri") AS ?groupClassIri)
    BIND(IRI("@projectIri") AS ?projectIri)

    FILTER NOT EXISTS { ?s knora-admin:groupName "@name"^^xsd:string . }
}
