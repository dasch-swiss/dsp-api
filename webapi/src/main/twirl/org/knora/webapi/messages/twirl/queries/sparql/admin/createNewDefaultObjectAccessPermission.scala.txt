@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@*
 * Creates a new default object access permission.
 * @param namedGraphIri           the name of the graph into which the new permission will be created.
 * @param permissionIri           the Iri of the new administrative permission.
 * @param permissionClassIri      the IRI of the OWL class that the new administrative permission should belong to.
 * @param projectIri              the project.
 * @param maybeGroupIri           the group's IRI.
 * @param maybeResourceClassIri   the resource's class IRI.
 * @param maybePropertyIri        the property's IRI.
 * @param permissions             the permission.
 *
 *@
@(namedGraphIri: IRI,
  permissionIri: IRI,
  permissionClassIri: IRI,
  projectIri: IRI,
  maybeGroupIri: Option[IRI],
  maybeResourceClassIri: Option[IRI],
  maybePropertyIri: Option[IRI],
  permissions: String)

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX knora-admin: <http://www.knora.org/ontology/knora-admin#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

INSERT {
    GRAPH ?namedGraphIri {
        ?permissionIri rdf:type ?permissionClassIri .

        ?permissionIri knora-admin:forProject ?projectIri .
        @if(maybeGroupIri.nonEmpty) {
            ?permissionIri knora-admin:forGroup <@maybeGroupIri.get> .
        }
        @if(maybeResourceClassIri.nonEmpty) {
            ?permissionIri knora-admin:forResourceClass <@maybeResourceClassIri.get> .
        }
        @if(maybePropertyIri.nonEmpty) {
            ?permissionIri knora-admin:forProperty <@maybePropertyIri.get> .
        }
        ?permissionIri knora-base:hasPermissions "@permissions"^^xsd:string .
    }
}

WHERE {
    BIND(IRI("@namedGraphIri") AS ?namedGraphIri)
    BIND(IRI("@permissionIri") AS ?permissionIri)
    BIND(IRI("@permissionClassIri") AS ?permissionClassIri)
    BIND(IRI("@projectIri") AS ?projectIri)
}
