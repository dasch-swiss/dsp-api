@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI
@import org.knora.webapi.messages.SmartIri
@import java.time.Instant

@**
 * Creates a new version of a value, with identical content except for new permissions.
 *
 * @param dataNamedGraph the named graph to update.
 * @param resourceIri the IRI of the resource containing the value.
 * @param propertyIri the IRI of the property that points from the resource to the value.
 * @param currentValueIri the IRI of the current version of the value.
 * @param newValueIri the IRI of the new version of the value.
 * @param newPermissions the new permissions on the value.
 * @param currentTime a timestamp that will be attached to the value.
 *@
@(dataNamedGraph: IRI,
  resourceIri: IRI,
  propertyIri: SmartIri,
  currentValueIri: IRI,
  valueTypeIri: SmartIri,
  newValueIri: IRI,
  newPermissions: String,
  currentTime: Instant)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

DELETE {
    GRAPH ?dataNamedGraph {
        @* Detach the current version of the value from the resource. *@
        ?resource ?property ?currentValue .

        @* Delete the resource's last modification date so we can update it. *@
        ?resource knora-base:lastModificationDate ?resourceLastModificationDate .

        @* Delete the UUID from the current version of the value, because the new version will store it. *@
        ?currentValue knora-base:valueHasUUID ?currentValueUUID .

        @* Delete the permissions from the current version of the value, because the new version will store them. *@
        ?currentValue knora-base:hasPermissions ?currentValuePermissions .
    }
} INSERT {
    GRAPH ?dataNamedGraph {
        @* Copy the contents of the current version of the value into the new version, along with the new permissions. *@
        ?newValue ?valuePred ?valueObj ;
            knora-base:previousValue ?currentValue ;
            knora-base:valueCreationDate "@currentTime"^^xsd:dateTime ;
            knora-base:hasPermissions "@newPermissions" .

        ?resource ?property ?newValue .

        @* Update the resource's last modification date. *@
        ?resource knora-base:lastModificationDate "@currentTime"^^xsd:dateTime .
    }
}

WHERE {
    BIND(IRI("@dataNamedGraph") AS ?dataNamedGraph)
    BIND(IRI("@resourceIri") AS ?resource)
    BIND(IRI("@propertyIri") AS ?property)
    BIND(IRI("@currentValueIri") AS ?currentValue)
    BIND(IRI("@newValueIri") AS ?newValue)

    ?resource rdf:type ?resourceClass ;
        knora-base:isDeleted false .

    ?resourceClass rdfs:subClassOf* knora-base:Resource .

    ?resource ?property ?currentValue .

    ?currentValue ?valuePred ?valueObj ;
        knora-base:isDeleted false ;
        knora-base:valueHasUUID ?currentValueUUID ;
        knora-base:hasPermissions ?currentValuePermissions .

    FILTER(!(?valuePred = knora-base:previousValue || ?valuePred = knora-base:valueCreationDate || ?valuePred = knora-base:hasPermissions))

    @* Get the resource's last modification date, if it has one, so we can update it. *@

    OPTIONAL {
        ?resource knora-base:lastModificationDate ?resourceLastModificationDate .
    }
}
