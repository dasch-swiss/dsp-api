@*
 * Copyright Â© 2015-2021 the contributors (see Contributors.md).
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Given a knora:base:internalFilename, retrieves the file value and information attached to it, using standard
 * SPARQL, without inference.
 *
 * This template is used only by getFileValue.scala.txt.
 *
 * @param triplestore the name of the triplestore being used.
 * @param fileValueIri the IRI of the file value.
 *@
@(triplestore: String,
  filename: String)

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

CONSTRUCT {
    ?fileValue ?objPred ?objObj .
    ?fileValue knora-base:attachedToProject ?resourceProject .
    ?fileValue knora-base:hasPermissions ?currentFileValuePermissions .
} WHERE {
    ?fileValue knora-base:internalFilename "@filename" .

    ?currentFileValue knora-base:previousValue* ?fileValue ;
        knora-base:hasPermissions ?currentFileValuePermissions .

    ?resource ?prop ?currentFileValue ;
        knora-base:attachedToProject ?resourceProject .

    ?fileValue ?objPred ?objObj .

    @* This FILTER is unnecessary, but it makes Jena run the query faster. *@
    FILTER(?objPred != knora-base:previousValue)

    ?currentFileValue knora-base:isDeleted false .
    ?resource knora-base:isDeleted false .
}
