@*
 * Copyright Â© 2021 - 2023 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi._
@import org.knora.webapi.messages.SmartIri

@*
 * Constructs a Gravsearch query that gets the metadata of a resource along with the values, if any, of the specified
 * properties.
 *
 * @param resourceIri the IRI of the resource.
 * @param propertyIris the IRIs of the properties to be queried.
 *@
@(resourceIri: IRI,
  propertyIris: Seq[SmartIri])

PREFIX knora-api: <http://api.knora.org/ontology/knora-api/v2#>

CONSTRUCT {
  ?resource knora-api:isMainResource true .

  @for((propertyIri, index) <- propertyIris.zipWithIndex) {
    ?resource <@propertyIri> ?propertyObj@index .
  }
} WHERE {
  BIND(<@resourceIri> AS ?resource)

  ?resource a knora-api:Resource .

  @for((propertyIri, index) <- propertyIris.zipWithIndex) {
    OPTIONAL {
      ?resource <@propertyIri> ?propertyObj@index .
    }
  }
}
