@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import dsp.errors.SparqlGenerationException
@import org.knora.webapi.messages.SmartIri
@import org.knora.webapi.messages.store.triplestoremessages.StringLiteralV2

@import java.time.Instant

@*
 * Changes the labels or comments of a class.
 *
 * @param ontologyNamedGraphIri the IRI of the named graph where the ontology is stored.
 * @param ontologyIri the IRI of the ontology to be modified.
 * @param classIri the IRI of the class to be modified.
 * @param predicateToUpdate rdfs:label or rdfs:comment.
 * @param newObjects the class's new labels or comments (a map of language codes to values).
 * @param lastModificationDate the xsd:dateTimeStamp that was attached to the ontology when it was last modified.
 * @param currentTime an xsd:dateTimeStamp that will be attached to the ontology.
 *@
@(ontologyNamedGraphIri: SmartIri,
  ontologyIri: SmartIri,
  classIri: SmartIri,
  predicateToUpdate: SmartIri,
  newObjects: Seq[StringLiteralV2],
  lastModificationDate: Instant,
  currentTime: Instant)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

DELETE {
    GRAPH ?ontologyNamedGraph {
        ?ontology knora-base:lastModificationDate "@lastModificationDate"^^xsd:dateTime .
        ?class <@predicateToUpdate> ?oldObj .
    }
} INSERT {
    GRAPH ?ontologyNamedGraph {
        ?ontology knora-base:lastModificationDate "@currentTime"^^xsd:dateTime .

        @for(newObject <- newObjects) {

            ?class <@predicateToUpdate> """@{newObject.value}"""@@@{newObject.language.getOrElse(throw SparqlGenerationException("No language code given in object of predicate $predicateToUpdate: $newObject"))} .

        }
    }
}

WHERE {
    BIND(IRI("@ontologyNamedGraphIri") AS ?ontologyNamedGraph)
    BIND(IRI("@ontologyIri") AS ?ontology)
    BIND(IRI("@classIri") AS ?class)

    GRAPH ?ontologyNamedGraph {
        ?ontology rdf:type owl:Ontology ;
            knora-base:lastModificationDate "@lastModificationDate"^^xsd:dateTime .

        ?class ?p ?o .

        OPTIONAL {
            ?class <@predicateToUpdate> ?oldObj .
        }
    }
}
