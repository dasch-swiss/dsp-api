@*
 * Copyright Â© 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@**
 * Given the IRI of a resource, gets the regions that point to this resource, using standard SPARQL, without
 * inference.
 *
 * This template is used only by getRegions.scala.txt.
 *
 * @param triplestore the name of the triplestore being used.
 * @param resourceIri the IRI of the resource.
 *@
 @(triplestore: String,
   resourceIri: IRI)

PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?region ?resclass ?regionObjectPermissions ?owner ?project
WHERE {
    BIND(IRI("@resourceIri") as ?resource)
    ?linkingProp rdfs:subPropertyOf* knora-base:isRegionOf .
    ?region ?linkingProp ?resource .

    ?resclass rdfs:subClassOf* knora-base:Region .
    ?region a ?resclass .

    ?region knora-base:attachedToUser ?owner ;
        knora-base:attachedToProject ?project ;
        knora-base:hasPermissions ?regionObjectPermissions .

    FILTER NOT EXISTS {
        ?resource knora-base:isDeleted true .
    }

    FILTER NOT EXISTS {
        ?region knora-base:isDeleted true .
    }
}
