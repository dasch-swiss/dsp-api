@*
 * Copyright Â© 2021 - 2024 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.messages.SmartIri
@import java.time.Instant
@import eu.timepit.refined.types.string.NonEmptyString

@*
 * Changes the metadata attached to an ontology.
 *
 * @param ontologyNamedGraphIri the IRI of the named graph where the ontology should be stored.
 * @param ontologyIri the IRI of the ontology to be created.
 * @param newLabel the ontology's new label.
 * @param hasOldComment true if the ontology has a comment.
 * @param deleteOldComment true if the existing comment should be deleted.
 * @param newComment the ontology's new comment.
 * @param lastModificationDate the xsd:dateTimeStamp that was attached to the ontology when it was last modified.
 * @param currentTime an xsd:dateTimeStamp that will be attached to the ontology.
 *@
@(ontologyNamedGraphIri: SmartIri,
  ontologyIri: SmartIri,
  newLabel: Option[String],
  hasOldComment: Boolean,
  deleteOldComment: Boolean,
  newComment: Option[NonEmptyString],
  lastModificationDate: Instant,
  currentTime: Instant)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

DELETE {
    GRAPH ?ontologyNamedGraph {
       @if(newLabel.nonEmpty) {
            ?ontology rdfs:label ?oldLabel .
        }
        @if(hasOldComment && (deleteOldComment || newComment.nonEmpty)) {
            ?ontology rdfs:comment ?oldComment .
        }
        ?ontology knora-base:lastModificationDate "@lastModificationDate"^^xsd:dateTime .

    }
} INSERT {
    GRAPH ?ontologyNamedGraph {
        @if(newLabel.nonEmpty) {
            ?ontology rdfs:label """@newLabel.get"""^^xsd:string .
        }
        @if(newComment.nonEmpty) {
            ?ontology rdfs:comment """@newComment.get.value"""^^xsd:string .
        }
        ?ontology knora-base:lastModificationDate "@currentTime"^^xsd:dateTime .
    }
}

WHERE {
    BIND(IRI("@ontologyNamedGraphIri") AS ?ontologyNamedGraph)
    BIND(IRI("@ontologyIri") AS ?ontology)

    GRAPH ?ontologyNamedGraph {
        ?ontology rdf:type owl:Ontology ;
        @if(newLabel.nonEmpty) {
            rdfs:label ?oldLabel ;
        }
        @if(hasOldComment) {
            rdfs:comment ?oldComment ;
        }
            knora-base:lastModificationDate "@lastModificationDate"^^xsd:dateTime .
    }
}
