@*
 * Copyright Â© 2015-2021 the contributors (see Contributors.md).
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Updates an existing group with the provided values.
 *
 * @param dataNamedGraph the named graph to update.
 * @param triplestore      the name of the triplestore being used. The template uses this value to exclude inferred
                           results from the WHERE clause of the update.
 * @param groupIri         the IRI of the group we want to update.
 * @param maybeName        the new optional value for name.
 * @param maybeDescription the new optional value for description.
 * @param maybeProject     the new optional value for project.
 * @param maybeStatus      the new optional value for status.
 * @param maybeSelfjoin    the new optional value for selfjoin.
 *@
@(adminNamedGraphIri: IRI,
  triplestore: String,
  groupIri: IRI,
  maybeName: Option[String],
  maybeDescription: Option[String],
  maybeProject: Option[IRI],
  maybeStatus: Option[Boolean],
  maybeSelfjoin: Option[Boolean])

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-admin: <http://www.knora.org/ontology/knora-admin#>

WITH <@adminNamedGraphIri>
DELETE {

    @* Delete current values, for which we have a new one. *@

    @if(maybeName.nonEmpty) {
        ?group knora-admin:groupName ?currentName .
    }

    @if(maybeDescription.nonEmpty) {
        ?group knora-admin:groupDescription ?currentDescription .
    }

    @if(maybeProject.nonEmpty) {
        ?group knora-admin:belongsToProject ?currentProject .
    }

    @if(maybeStatus.nonEmpty) {
        ?group knora-admin:status ?currentStatus .
    }

    @if(maybeSelfjoin.nonEmpty) {
        ?group knora-admin:hasSelfJoinEnabled ?currentSelfjoin .
    }



} INSERT {

    @* Add the new values. *@

    @if(maybeName.nonEmpty) {
        ?group knora-admin:groupName "@maybeName.get"^^xsd:string .
    }

    @if(maybeDescription.nonEmpty) {
        ?group knora-admin:groupDescription """@maybeDescription.get"""^^xsd:string .
    }

    @if(maybeProject.nonEmpty) {
        ?group knora-admin:belongsToProject <@maybeProject.get> .
    }

    @if(maybeStatus.nonEmpty) {
        ?group knora-admin:status "@maybeStatus.get"^^xsd:boolean .
    }

    @if(maybeSelfjoin.nonEmpty) {
        ?group knora-admin:hasSelfJoinEnabled "@maybeSelfjoin.get"^^xsd:boolean .
    }
}

@*

GraphDB's consistency checking requires reasoning, but reasoning interferes with certain things
in the WHERE clauses of our SPARQL updates, so we set a GraphDB-specific flag to return only
explicit statements in the WHERE clause here.

*@

@triplestore match {
    case "graphdb" | "graphdb-free" => {
        USING <http://www.ontotext.com/explicit>
    }

    case other => {}
}

WHERE {
    BIND(IRI("@groupIri") AS ?group)

    @* Get all current defined values. *@

    ?group knora-admin:groupName ?currentName .

    optional {?group knora-admin:groupDescription ?currentDescription .}

    ?group knora-admin:belongsToProject ?currentProject .

    ?group knora-admin:status ?currentStatus .

    ?group knora-admin:hasSelfJoinEnabled ?currentSelfjoin .
}
